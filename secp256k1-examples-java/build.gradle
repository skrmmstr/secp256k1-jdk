plugins {
    id 'java'
    id 'application'
}

tasks.withType(JavaCompile).configureEach {
    //options.release = 22
}

ext.moduleName = 'org.bitcoinj.secp256k1.examples'

dependencies {
    implementation project(':secp256k1-api')
    runtimeOnly project(':secp256k1-bouncy')
    runtimeOnly project(':secp256k1-foreign')

    // This is only needed for ForeignRegistrationFeature and the native-image build
    implementation group: 'org.graalvm.sdk', name: 'nativeimage', version: '24.0.0'
}

jar {
    inputs.property("moduleName", moduleName)
    manifest {
        attributes  'Implementation-Title': 'Example secp256k1-jdk Apps',
                'Implementation-Version': archiveVersion.get()
    }
}

application {
    mainModule = 'org.bitcoinj.secp256k1.examples'
//    mainClass = 'org.bitcoinj.secp256k1.examples.Ecdsa'
    mainClass = 'org.bitcoinj.secp256k1.examples.Schnorr'
}

run {
    def userHome = System.getProperty("user.home")
    systemProperty "java.library.path", findProperty("javaPath") ?: "${userHome}/.nix-profile/lib"
    jvmArgs += '--enable-native-access=org.bitcoinj.secp256k1.foreign'
}

configurations {
    nativeToolImplementation.extendsFrom implementation
}

def mainClassName = "org.bitcoinj.secp256k1.examples.Schnorr"

jar {
    manifest {
        attributes  'Implementation-Title': 'Schnorr Signature Example',
                'Main-Class': mainClassName,
                'Implementation-Version': archiveVersion.get()
    }
}

// Compile a native image using GraalVM's native-image tool
// Graal must be installed at $GRAALVM_HOME
tasks.register('nativeCompile', Exec) {
    dependsOn jar
    workingDir = projectDir
    executable = "${System.env.GRAALVM_HOME}/bin/native-image"
    args = ['--verbose',
            '--no-fallback',
            '-cp', "${-> configurations.nativeToolImplementation.asPath}", // Lazy configuration resolution
            '-jar', jar.archiveFile.get(),
            '-H:Path=build',
            '-H:Name=schnorr',
            '-H:+ForeignAPISupport',
            '--features=org.bitcoinj.secp256k1.examples.ForeignRegistrationFeature',
            '--enable-native-access=ALL-UNNAMED',
            '-H:+ReportUnsupportedElementsAtRuntime',
            '-H:+ReportExceptionStackTraces'
    ]
}

